#EXPORT=COUNTTITLELENGTH.IN:TITLE
#EXPORT=SPLIT.IN:SENTENCE
#EXPORT=SETSCORE.OUT:OUT

EXPORT=TITLE.IN:TITLE
EXPORT=SENTENCE.IN:SENTENCE
EXPORT=RESULT.OUT:OUT

Title(core/Repeat) OUT -> IN CountTitleLength(packets/Counter)
Sentence(core/Repeat) OUT -> IN Split(core/Split)
SetScore(objects/SetPropertyValue) OUT -> IN Result(core/Repeat)

# Configuration
'sentence' -> KEY ExtractSentence(objects/ExtractProperty)
'score' -> KEY ExtractScore(objects/ExtractProperty)
'score' -> PROPERTY SetScore(objects/SetPropertyValue)
'1.5' -> MULTIPLIER Multiply(math/Multiply)

Split(core/Split) OUT -> IN ExtractSentence()

# Count Words of Title
CountTitleLength() OUT -> ACCEPT Guard(flow/Accept)
Split() OUT -> IN ExtractScore()
ExtractSentence() OUT -> IN SplitArray(objects/SplitArray)
SplitArray() OUT -> IN Guard(flow/Accept)
Guard() OUT -> IN Counter(packets/Counter)
Counter() OUT -> IN Drop(core/Drop)

# Calculate Score
CountTitleLength() COUNT -> DIVISOR Divide(math/Divide)
Counter() COUNT -> DIVIDEND Divide(math/Divide)
Divide() QUOTIENT -> MULTIPLICAND Multiply()


# Update Score
Multiply() PRODUCT -> ADDEND Add(math/Add)
ExtractScore() OUT -> AUGEND Add()

# Update IP
Split() OUT -> IN SetScore()
Add() SUM -> VALUE SetScore()
