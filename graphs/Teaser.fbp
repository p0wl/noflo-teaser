# SUBGRAPH DEFINITION
EXPORT=TOP.LENGTH:TOP
EXPORT=TITLEFORWARD.IN:TITLE
EXPORT=TEXTFORWARD.IN:CONTENT
EXPORT=WRAPPERKEYWORDS.OUT:OUT

# Configuration
'score' -> PROPERTY SortBy(underscore/SortBy)
'desc' -> ORDER SortBy()
'sentence' -> KEY Extractor(objects/ExtractProperty)
'. ' -> DELIMITER SplitSentences(strings/SplitStr)
' ' -> DELIMITER SplitTitleWords(strings/SplitStr)
' ' -> DELIMITER SplitTextWords(strings/SplitStr)

# Tolowercase TODO
TextForward(teaser/Forward) OUT -> INTEXT Summarize(teaser/Summarize)
TitleForward(teaser/Forward) OUT -> INTITLE Summarize()

# SPLIT Text for multiple Components
Summarize() TEXT -> IN Text(core/Split)

# Split Text to Sentences
Text() OUT -> IN SplitSentences()
# First Wrapper
SplitSentences() OUT -> IN Structure(teaser/StructureSentenceIp)
Structure() OUT -> IN DisconnectAfterPacket(core/DisconnectAfterPacket) OUT -> IN Wrapper(teaser/ScoreStepWrapper)

# Get Words from Title
Summarize() TITLE -> IN SplitTitleWords() OUT -> IN FilterBadwordsTitle(teaser/FilterBadwords)

# Filter Badwords from Text
Text() OUT -> IN SplitTextWords()
SplitTextWords() OUT -> IN CountTextWords(packets/Counter) OUT -> IN FilterBadwords(teaser/FilterBadwords)

# Get Keywords
FilterBadwords() OUT -> IN ExtractKeywords(teaser/ExtractKeywords)
CountTextWords() COUNT -> LENGTH ExtractKeywords()

# ScoreByTitle
FilterBadwordsTitle() OUT -> TITLE ScoreByTitle(teaser/ScoreByTitle)
Wrapper() SENTENCE -> IN ScoreByTitle()
ScoreByTitle() OUT -> SCORE Wrapper()

# Results from ScoreByTitle -> Input ScoreByKeywords
Wrapper() OUT -> IN Stop(flow/Stop)
Stop() OUT -> IN WrapperKeywords(teaser/ScoreStepWrapper)

# ScoreByKeywords
ExtractKeywords() OUT -> IN SplitForStop(core/Split) OUT -> KEYWORDS ScoreByKeywords(teaser/ScoreByKeywords)
WrapperKeywords() SENTENCE -> IN ScoreByKeywords()
ScoreByKeywords() OUT -> SCORE WrapperKeywords()

SplitForStop() OUT -> READY Stop()



